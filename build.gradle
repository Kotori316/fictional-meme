import com.google.gson.Gson
import com.google.gson.JsonObject

import java.util.function.Predicate
import java.util.regex.Matcher
import java.util.regex.Pattern

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath("com.google.code.gson:gson:2.8.6")
    }
}
apply plugin: 'net.minecraftforge.gradle'
version = '1.0'
group = 'com.kotori316' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'modid'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    runs {
        data {
            workingDirectory project.file('run')
            forceExit false
            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'
            property "mixin.debug.export", "true"

            String mods = System.getenv("CI_MODS") ?: getProperty("ci.mods") ?: ""
            args '--mod', mods, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

repositories {
    maven {
        name = "Azure-SLP"
        url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
        content {
            includeModule("com.kotori316", "ScalableCatsForce".toLowerCase())
            includeModule("com.kotori316", "ModJar")
        }
    }
    repositories.stream().filter { it instanceof MavenArtifactRepository }.forEach { repo ->
        repo.content {
            excludeVersionByRegex(".*", ".*", ".*_mapped_snapshot_.*")
        }
    }
}

dependencies {
    String forgeVersion = java.util.Optional.ofNullable(System.getenv("CI_FORGE") ?: getProperty("forge.version"))
            .filter(Predicate.isEqual("default").negate())
            .filter(Predicate.isEqual("latest").negate())
            .orElseGet({ getLatestForge(null) })
    String forgeArtifact = forgeVersion.startsWith("net.minecraftforge:forge:") ? forgeVersion : "net.minecraftforge:forge:$forgeVersion"

    minecraft(forgeArtifact)

    def scala_version = "2.13.5"
    implementation "org.scala-lang:scala-library:${scala_version}"
    // A library of functional programing in Scala
    implementation group: 'org.typelevel', name: "cats-core_" + scala_version.substring(0, scala_version.lastIndexOf(".")), version: '2.4.2'

    implementation(group: 'com.kotori316', name: 'ScalableCatsForce'.toLowerCase(Locale.ROOT), version: '2.13.4-build-2', classifier: 'dev')

    String JUPITER_VERSION = System.getenv("JUPITER_VERSION") ?: '5.7.0'
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.0')
    runtimeOnly(group: "com.kotori316", name: "ModJar", version: "2.0.0") {
        transitive(false)
    }
}

static String getLatestForge(String key) {
    def target = "https://files.minecraftforge.net/maven/net/minecraftforge/forge/promotions_slim.json"
    URL apiUrl = new URL(target)
    InputStream inputStream = apiUrl.openStream()
    Gson gson = new Gson()
    JsonObject allList = gson.fromJson(new InputStreamReader(inputStream), JsonObject.class).getAsJsonObject("promos")
    if (key == null) {
        Pattern pattern = Pattern.compile("(1\\.(\\d+)(|\\.(\\d+)))-latest")
        return allList.keySet().stream().map { s -> pattern.matcher(s) }
                .filter { m -> m.matches() }
                .map { m ->
                    int major = Integer.parseInt(m.group(2))
                    int minor = m.group(4) == null || m.group(4).isEmpty() ? 0 : Integer.parseInt(m.group(4))
                    return new AbstractMap.SimpleImmutableEntry<Integer, Matcher>(major * 100 + minor, m)
                }
                .sorted(Map.Entry.comparingByKey().reversed() as Comparator<? super AbstractMap.SimpleImmutableEntry<Integer, Matcher>>)
                .map { e -> e.getValue() }
                .findFirst()
                .map { k -> k.group(1) + "-" + allList.get(k.group()).getAsString() }
                .orElse(null)
    } else {
        String prefix = key.split("-")[0]
        return prefix + "-" + allList.get(key).getAsString()
    }
}

task printForgeVersions() {
    doLast {
        def versions = [
                "1.15.1-latest",
                "1.16.4-latest",
                "1.16.5-latest",
                "1.12.2-latest",
                "1.12-latest",
                "1.8-latest",
                "1.7.2-latest",
                "1.7.10-latest",
                null
        ]
        for (String s : versions) {
            println("$s: ${getLatestForge(s)}")
        }
    }
}
