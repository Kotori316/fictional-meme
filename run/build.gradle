buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
version = '1.0'
group = 'com.kotori316' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'modid'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    if (System.getenv("PARCHMENT_ENABLED") == null) {
        mappings channel: project.mapping_channel, version: project.mapping_version
    } else {
        mappings channel: project.mapping_channel, version: project.parchment_mapping
    }

    runs {
        data {
            workingDirectory project.file('run')
            forceExit false
            property('forge.logging.markers', 'REGISTRIES,REGISTRYDUMP')
            property('forge.logging.console.level', 'debug')
            property('mixin.env.remapRefMap', 'true')
            property('mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg")
            property("mixin.debug.export", "true")

            String mods = System.getenv("CI_MODS") ?: getProperty("ci.mods") ?: ""
            args('--mod', mods, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'))
        }
    }
}

repositories {
    maven {
        name = "Azure-Kotori316"
        url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
        content {
            it.includeModule("com.kotori316", "ScalableCatsForce".toLowerCase())
            it.includeModule("com.kotori316", "ModJar")
            it.includeModule("org.typelevel", "cats-core_2.13")
            it.includeModule("org.typelevel", "cats-kernel_2.13")
        }
    }
    repositories.stream().filter { it instanceof MavenArtifactRepository }.forEach { repo ->
        repo.content {
            excludeVersionByRegex(".*", ".*", ".*_mapped_snapshot_.*")
        }
    }
}

dependencies {
    String forgeVersion = System.getenv("CI_FORGE") ?: getProperty("forge.version")
    String forgeArtifact = forgeVersion.startsWith("net.minecraftforge:forge:") ? forgeVersion : "net.minecraftforge:forge:$forgeVersion"

    minecraft(forgeArtifact)

    def scala_version = "2.13.7"
    implementation "org.scala-lang:scala-library:${scala_version}"
    // A library of functional programing in Scala
    implementation group: 'org.typelevel', name: "cats-core_" + scala_version.substring(0, scala_version.lastIndexOf(".")), version: '2.6.2-kotori'

    compileOnly(group: 'com.kotori316', name: 'ScalableCatsForce'.toLowerCase(Locale.ROOT), version: '2.13.7-build-1', classifier: 'dev')
    runtimeOnly(group: 'com.kotori316', name: 'ScalableCatsForce'.toLowerCase(Locale.ROOT), version: '2.13.7-build-1', classifier: 'with-library')

    String JUPITER_VERSION = System.getenv("JUPITER_VERSION") ?: '5.7.1'
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUPITER_VERSION)
    runtimeOnly(group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.0')
    runtimeOnly(group: "com.kotori316", name: "ModJar", version: "2.0.1") {
        transitive(false)
    }
}

task printProperties() {
    doLast {
        List<String> list = []
        // Version
        list.add("CI_FORGE: ${System.getenv("CI_FORGE")}")
        list.add("forge.version: ${getProperty("forge.version")}")
        list.add("mapping_channel: ${project.mapping_channel}")
        list.add("mapping_version: ${project.mapping_version}")
        list.add("parchment_mapping: ${project.parchment_mapping}")
        list.add("ci.mods: ${getProperty("ci.mods")}")
        list.add("")
        list.add("java.version: ${System.getProperty("java.version")}")

        list.forEach { s -> System.out.println(s) }
    }
}
